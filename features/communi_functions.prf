######################################################################
# Communi
######################################################################

isEmpty(QMAKE_EXTENSION_SHLIB) {
    win32:QMAKE_EXTENSION_SHLIB = dll
    else:QMAKE_EXTENSION_SHLIB = so
}

defineTest(communiAddLib) {
    LIBNAME = $$1
    isEmpty(LIBNAME):error(LIBNAME must be set)
    LIBS *= -L$$BUILD_TREE/lib
    LIBS *= -l$$LIBNAME
    PRE_TARGETDEPS *= $$BUILD_TREE/lib/$${QMAKE_PREFIX_SHLIB}$${LIBNAME}.$${QMAKE_EXTENSION_SHLIB}
    export(LIBS)
    export(PRE_TARGETDEPS)
}

defineTest(communiAddPlugin) {
    PLUGINNAME = $$1
    isEmpty(PLUGINNAME):error(PLUGINNAME must be set)
    LIBS *= -L$$BUILD_TREE/plugins
    LIBS *= -l$${PLUGINNAME}plugin
    PRE_TARGETDEPS *= $$BUILD_TREE/plugins/$${QMAKE_PREFIX_SHLIB}$${PLUGINNAME}plugin.$${QMAKE_EXTENSION_SHLIB}
    export(LIBS)
    export(PRE_TARGETDEPS)
}
